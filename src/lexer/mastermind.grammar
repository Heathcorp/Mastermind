@precedence {preprocess @left, summation}

@tokens { 
    whitespace {
        ($[ \t\r\n] | "\\" "\r"? "\n")+
    }
    Comment { "//" ![\n]* }

    nameChar { @asciiLetter | "_" }
    Name {
        nameChar (nameChar | @digit)*
    }


    IncludePath {
        "<" (![\n\\] | "\\" _)+ ">"
    }
    
    Number {
        (@digit)+
    }
    
    String {
        "\"" !["]* "\""
    }

    
    Character {
        "\'" !['] ![']? "\'"
    }
    
    // newline { "\n" }
    Semicolon { ";" }
    Comma { "," }
    EqualOp { "=" }
    AddEqualOp { "+=" | "-=" }
    AddOp { "+" | "-" }
    IncDecOp { "++" | "--" }
    Asterisk { "*" }
    At { "@" }

    BfPlus { "+" }
    BfMinus { "-" }
    BfRight { ">" }
    BfLeft { "<" }
    BfOpenLoop { "[" }
    BfCloseLoop { "]" }
    BfOutput { "." }
    BfInput { "," }
    // BfComment { (![\\/#+-><.,{}\][ \t\n\r])+ }
}

kw<word> { @specialize<Name, word> }


commaSep<content> {
    "" | content (Comma content?)*
}

Boolean { kw<"true" | "false"> }

VariableDefinition {
    Name SquareBrackets<Number>?
}
VariableTarget {
    Name SquareBrackets<Number>? |
    Asterisk Name
}

LocationSpecifier { At Number }

Constant {
    Number | Boolean | Character
}

Expression {
    VariableTarget |
    Constant |
    String |
    Array { SquareBrackets<commaSep<Expression>> } |
    Parentheses<Expression> |
    (Expression AddOp !summation Expression)
}


EBrainfuck {
    (
        BfPlus |
        BfMinus |
        BfRight |
        BfLeft |
        BfOpenLoop |
        BfCloseLoop |
        BfOutput |
        BfInput |
        Block
        // | BfComment
    )*
}

Block {
    Braces<Clause*>
}

Parentheses<contents> {
    "("
        contents
    ")"
}

SquareBrackets<contents> {
    "[" 
        contents
    "]"
}

AngledBrackets<contents> {
    "<"
        contents
    ">"
}

Braces<contents> {
    "{"
        contents
    "}"
}

Clause {
    Empty { "" Semicolon} |

    DefClause {
        Def { kw<"def"> } Name AngledBrackets<commaSep<VariableDefinition>> Block
    } |

    CallClause {
        Name AngledBrackets<commaSep<VariableTarget>> Semicolon
    } |

    LetClause {
        Let { kw<"let"> } VariableDefinition LocationSpecifier? (EqualOp Expression)? Semicolon
    } |

    SetClause {
        VariableTarget EqualOp Expression Semicolon
    } |

    IncDec {
        IncDecOp VariableTarget Semicolon
    } |

    Add {
        VariableTarget AddEqualOp Expression Semicolon
    } |

    DrainCopyClause {
        DrainCopy { kw<"drain"> | kw<"copy"> } Expression (Into { kw<"into"> } VariableTarget+)? (Block | Semicolon)
    } |

    WhileClause {
        While { kw<"while"> } VariableTarget Block
    } |

    IfElseClause {
        If { kw<"if"> } Not { kw<"not"> }? Expression Block (Else { kw<"else"> } Block)?
    } |

    OutputClause { Output { kw<"output"> } Expression Semicolon } |
    InputClause { Input { kw<"input"> } VariableTarget Semicolon } |

    BrainfuckClause {
        Bf { kw<"bf"> } LocationSpecifier? (Clobbers { kw<"clobbers"> } VariableTarget+)? Braces<EBrainfuck>
    } |
    AssertClause {
        Assert { kw<"assert"> } VariableTarget ((Equals { kw<"equals"> } Constant) | Unknown { kw<"unknown"> }) Semicolon
    }
}

PreprocessorDirective {
    IncludeDirective {
        Include { "#include" } ( IncludePath | String )
    } 
}

@top File {
    Clause*
}

@skip { 
    PreprocessorDirective |
    whitespace |
    Comment
}
